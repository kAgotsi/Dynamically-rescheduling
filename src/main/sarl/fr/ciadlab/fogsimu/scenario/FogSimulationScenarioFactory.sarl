/* 
 * $Id$
 * 
 * Copyright (c) 2019 The original authors, and other authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package fr.ciadlab.fogsimu.scenario

import fr.ciadlab.fogsimu.config.Configuration
import fr.ciadlab.fogsimu.environment.FogEnvironmentModel
import fr.ciadlab.fogsimu.environment.FogZone
import io.sarl.lang.core.Agent
import io.sarl.lang.util.OutParameter
import java.util.Collection
import java.util.List
import java.util.Map
import java.util.UUID
import org.arakhne.afc.gis.maplayer.MapElementLayer
import org.arakhne.afc.gis.maplayer.TreeMapElementLayer
import org.arakhne.afc.gis.road.layer.RoadNetworkLayer
import org.arakhne.afc.simulation.framework.base.probes.ProbeManager
import org.arakhne.afc.simulation.framework.framework1d.environment.EnvironmentModel1d
import org.arakhne.afc.simulation.framework.framework1d.environment.Spawner1d
import org.arakhne.afc.simulation.framework.framework1d.scenario.SimulationScenarioFactory1d
import java.io.File
import java.util.ResourceBundle
import javafx.scene.chart.XYChart

/**
 * Reader of Simulation scenario.
 *
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
class FogSimulationScenarioFactory extends SimulationScenarioFactory1d<Configuration> {

	new {
		super(new XmlReader)
	}

	/** Read fog areas from the simulation configuration and include them into the simulation scenario.
	 * 
	 * @param configuration the simulation configuration.
	 * @param loadedRoads the layer that contains the loaded roads.
	 * @param containers the collection of layers that must be included into the simulation. 
	 */
	protected def loadFogAreas(configuration : Configuration, loadedRoads : RoadNetworkLayer,
		containers : List<MapElementLayer<?>>) : void {
		var fogLayer = new TreeMapElementLayer<FogZone>
		for (obj : configuration.fogZones) {
			var fogZone = new FogZone(obj.ID, obj.x, obj.y, obj.radius, obj.visibility)
			fogLayer.addMapElement(fogZone)
			
			// The following code put the references to the fog zones on the road segments
			var segmentIterator = loadedRoads.roadNetwork.iterator(fogZone.boundingBox)
			while (segmentIterator.hasNext) {
				var segment = segmentIterator.next
				if (fogZone.shape.intersects(segment.toPath2D)) {
					var oldFogZone : FogZone = segment.getUserData(FogEnvironmentModel::FOG_ZONE_REFERENCE_ATTRIBUTE_NAME)
					if (oldFogZone === null || oldFogZone.visibility > fogZone.visibility) {
						segment.setUserData(FogEnvironmentModel::FOG_ZONE_REFERENCE_ATTRIBUTE_NAME, fogZone)
					}
				}
			}
		}
		if (!fogLayer.isEmpty) {
			fogLayer.visible = false
			containers.add(fogLayer)
		}
	}

	override buildSimulationScenario(root : File, configuration : Configuration,
		bundle : ResourceBundle, loadedRoads : RoadNetworkLayer,
		containers : List<MapElementLayer<?>>, objectAgents : Collection<Map<UUID, Class<? extends Agent>>>,
		spawners : List<Spawner1d>, probeManager : OutParameter<ProbeManager>,
		charts : Map<UUID, XYChart<Double, Double>>) : void {
		super.buildSimulationScenario(
			root, configuration, bundle, loadedRoads, containers,
			objectAgents, spawners, probeManager, charts)
		loadFogAreas(configuration, loadedRoads, containers)
	}

	protected override createEnvironmentModel(roads : RoadNetworkLayer) : EnvironmentModel1d {
		new FogEnvironmentModel(roads)
	}

}
